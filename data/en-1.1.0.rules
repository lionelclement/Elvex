
/* **************************************************
 * 
 ************************************************** */
Axiom → Text_with_features strongPunct {
  ↓1 = ↑;
  [punct:$punc] ⊂ ⇓1;
  ↓2 = [punct:$punc];
}

/* **************************************************
 * 
 ************************************************** */
@withspaces
Text_with_features → VTENSE S_tense {
  [vtense:$vtense] ⊂ ⇓2;
  [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest] ⊂ ↑;
  if (¬ $lexical_function)
    $lexical_function = none;
  //if ($tense ≠ _)
  //  $tense = present;
  //else
  //  $tense = past;
  if ((¬ $tense))
    $tense = present;
  // not interrogative, not negative
  if (¬ $verbal_constr)
    $verbal_constr = no_int | no_neg;
    [vtense:$vtenseSth] ⊂ ⇓2;
  attest($vtenseSth != infinitive);
  ↓1 = [vtense:$vtenseSth];
  ↓2 = [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest];
  ⇑ = ⇓2;
}

Text → S_tense strongPunct {
  [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest] ⊂ ↑;
  if (¬ $lexical_function)
    $lexical_function = none;
  if (¬ $tense)
    $tense = present;
  if (¬ $verbal_constr)
    $verbal_constr = no_int | no_neg;
  ↓1 = [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest];
  [punct:$punc] ⊂ ⇓1;
  ↓2 = [punct:$punc];
}

Text → NP {
  [lexical_function:$lexical_function, $Rest] ⊂ ↑;
  if (¬ $lexical_function)
    $lexical_function = none;
  ↓1 = [lexical_function:$lexical_function, $Rest];
}

/* **************************************************
 * 
 ************************************************** */
S_tense → S_anaphoric {
  [tense:$tense, $Rest];
  [mode:$mode, vtense:$vtense, aspect:$aspect] ⊂ ↑;

  if (¬ $tense)
    $tense = present;

  if (¬ $mode)
    $mode = indicative;

  if (¬ $aspect)
    $aspect = default;

  if (¬ $vtense){
    if ($mode == indicative) {
    // $Present
    if ($tense == present){
      if ($aspect == continuous)
        $vtense=present_continuous;
      else
        $vtense=present;
    }
    // Past
    else if ($tense == past){
      if ($aspect == continuous)
        $vtense=past_continous;
      else if ($aspect == perfect)
        $vtense=present_perfect;
      else
        $vtense=preterite;
    }
    // Future
    else if ($tense == future){
      if ($aspect == continuous)
        $vtense=future_continuous;
      else
        $vtense=future;
    }
    // Else
    else
      $vtense=$tense;
  }
  }
  
  //↓1 = [vtense:$vtense, $Rest];
  ↓1 = [vtense:$vtense] ∪ $Rest;
  ⇑ = ⇓1;
}

/* **************************************************
* [i:[ID:$ID1], ii:[ii:[i:[HEAD:$H2, ID:$ID2, $RII_II_I], $RII_II], $R_II], $R]
* [i:$I] ⊂ ↑;
* =>
* [i:$I, ii:[ii:[i:[HEAD:pro, ID:$ID2, $RII_II_I], $RII_II], $R_II], $R]
 ************************************************** */
S_anaphoric → S_mod {
  [i:[ID:$ID1], ii:[ii:[i:[HEAD:$H2, ID:$ID2, $RII_II_I], $RII_II], $R_II], $R];
  attest ($ID1 == $ID2);
  [i:$I] ⊂ ↑;
  ↓1 = [i:$I, ii:[ii:[i:[HEAD:pro, ID:$ID2, $RII_II_I], $RII_II], $R_II], $R];
  ⇑ = ⇓1;
}

S_anaphoric → S_mod {
  [i:[ID:$ID1], ii:[ii:[i:[HEAD:$H2, ID:$ID2, $RII_II_I], $RII_II], $R_II], $R] ⊂ ↑;
  attest (¬$ID1 ∨ ¬$ID2 ∨ $ID1 != $ID2);
  ↓1 = ↑;
  ⇑ = ⇓1;
}

/* **************************************************
 * Sentence modifiers
 ************************************************** */
@withspaces
S_mod → S_mod form {
  [MOD:<$H :: $T>, $Rest];
  ↓1 = [MOD:$T, $Rest];
  ↓2 = $H;
  ⇑ = ⇓1;
}

@withspaces
S_mod → S_mod advs {
  [MOD:<$H :: $T>, $Rest];
  ↓1 = [MOD:$T, $Rest];
  ↓2 = $H;
  ⇑ = ⇓1;
}

S_mod → S {
  [MOD:NIL, $Rest];
  ↓1 = $Rest;
  ⇑ = ⇓1;
}

/* **************************************************
 * Cause : conclusion when premise
 ************************************************** */
@withspaces
S → S_anaphoric form S_anaphoric {
  [HEAD:CAUSE, i:$premise, ii:$conclusion, $Rest];


  [i:[i:$I_premise, $Rest_premise]] ⊂ ↑; // $I_premise = the agent of the premise
  [ii:[i:$I_conclusion, $Rest_conclusion]] ⊂ ↑; // $I_conclusion = the agent of the conclusion
  [i:[i:[ID:$ID_I_premise]]] ⊂ ↑; // $ID_I_premise = the agent ID of the premise
  [ii:[i:[ID:$ID_I_conclusion]]] ⊂ ↑; // $ID_I_conclusion = the agent ID of the conclusion
  
  [vtense:$vtense, $Rest_vtense] ⊂ $Rest;

  if (¬ $vtense ∨ ($vtense == present)) {
    ↓2 = [FORM:"now that"];
    $vtensePremise = present_continuous;
    $vtenseConclusion = present_continuous;
  }
  else if ($vtense == preterite) {
    ↓2 = [FORM:"when"];
    $vtensePremise = past_continous;
    $vtenseConclusion = past_continous;
  }
  else if ($vtense == future) {
    ↓2 = [FORM:"when"];
    $vtensePremise = future_continuous;
    $vtenseConclusion = future_continuous;
  }

  // If the agent's ID exists in both of the two clauses and they refer to the same value
  if ($ID_I_premise ∧ $ID_I_conclusion ∧ ($ID_I_premise == $ID_I_conclusion)) { 
    [i:[HEAD:_, $Synth_I_conclusion]] ⊂ ⇓1; // Provide the characteristics of the realized agent,
                                      //excluding its head that has been synthesized.
    $Inh_I_conclusion = [i:[HEAD:pro, ID:$ID_I_premise, $Synth_I_conclusion]]; //Generate a pronoun to refer to the agent of the conclusion
    ↓1 = $Rest_conclusion ∪ [i:$I_premise] ∪ [vtense:$vtenseConclusion] ∪ $Rest_vtense; // The first clause.
    ↓3 = $Rest_premise ∪ $Inh_I_conclusion ∪ [vtense:$vtensePremise] ∪ $Rest_vtense; // The second clause.
  }
  else {
    ↓1 = $conclusion ∪ $Rest; // The first clause.
    ↓3 = $premise ∪ $Rest; // The second clause.
  }
  ⇑ = ⇓2 ∪ [punct:_dot_];
}

/* **************************************************
 * Sentence arguments
 ************************************************** */
S → S {
  [i:$i, ii:NIL, iii:NIL, $Rest];
  ↓1 = [SUBJECT:$i, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:NIL, $Rest];
  ↓1 = [SUBJECT:$i, OBJECT:$ii, iiReal:OBJECT, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:NIL, $Rest];
  ↓1 = [SUBJECT:$i, PRED_ADJ:$ii, iiReal:PRED_ADJ, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:NIL, $Rest];
  ↓1 = [SUBJECT:$i, OBJ_THETA:$ii, iiReal:OBJ_THETA, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:NIL, $Rest];
  ↓1 = [SUBJECT:$i, SCOMP:$ii, iiReal:SCOMP, $Rest];
  ⇑ = ⇓1;
}

// Infinitive subject controller
@trace
S → S {
  [i:$i, ii:$ii, iii:NIL, $Rest];
  [i:[HEAD:$H1, ID:$ID1], ii:[i:[HEAD:$H2, ID:$ID2], $iiRest]] ⊂ ↑;
  attest (¬$H1 ∨ ¬$H2 ∨ ($H1 == $H2));
  attest ($ID1 == $ID2);
  ↓1 = [SUBJECT:$i, VCOMP:[i:$i, $iiRest]/*, iiReal:VCOMP*/, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:$iii, $Rest];
  ↓1 = [SUBJECT:$i, OBJECT:$ii, iiReal:OBJECT, OBJ_THETA:$iii, iiiReal:OBJ_THETA, $Rest];
  ⇑ = ⇓1;
}

S → S {
  [i:$i, ii:$ii, iii:$iii, $Rest];
  ↓1 = [SUBJECT:$i, OBJECT:$ii, iiReal:OBJECT, SCOMP:$iii, iiiReal:SCOMP, $Rest];
  ⇑ = ⇓1;
}

/* **************************************************
 * Affirmative Sentence
 ************************************************** */
@withspaces
S → [NP|cln] VP {
  [SUBJECT:$SUBJECT, vtense:$vtense, $Rest];
  [verbal_constr:$verbal_constr] ⊂ ↑;
  if (#1){
    attest ($vtense != infinitive);
    ↓1 = $SUBJECT ∪ [case:nominative];
    $Synth_Subject = ⇓1;
    ↓2 = [SUBJECT:$Synth_Subject, vtense:$vtense, tensed:true, $Rest];
    [SUBJECT:$expected_subject] ⊂ ⇓2;
    attest $Synth_Subject ∪ $expected_subject;
  }
  else {
    attest ($vtense == infinitive);
    ↓2 = [vtense:$vtense, $Rest];
  }
    // not Interrogative form
  attest ($verbal_constr == no_int);
  $iSynth = ⇓1;
  ⇑ = ⇓2 ∪ [i:$iSynth, punct:_dot_];
}

/* **************************************************
 * Exclamatory Sentence
 ************************************************** */
S → VP {
  [SUBJECT:$SUBJECT, verbal_constr:$verbal_constr, vtense:$vtense, $Rest];
  // not Interrogative form
  attest (($verbal_constr == excl));
  ↓1 = [SUBJECT:[@_2s], vtense:present] ∪ [SUBJECT:$SUBJECT, vtense:$vtense, tensed:true, verbal_constr:$verbal_constr, $Rest];
  ⇑ = ⇓1 ∪ [form:excl];
}

/* **************************************************
 * Interrogative Sentence
 ************************************************** */
@withspaces
S → aux NP|cln VP strongPunct {
  [SUBJECT:$SUBJECT, vtense:$vtense, $Rest];
  [verbal_constr:$verbal_constr] ⊂ ↑;
  ↓2 = $SUBJECT ∪ [case:nominative];
  $Synth_Subject = ⇓2;
  // Interrogative form
  attest (($verbal_constr == int));
  ↓1 = ↑ ∪ [SUBJECT:$Synth_Subject, tensed:true];
  [verbal_constr:$synth_verbal_constr, aux_form:$aux_form, verb_vform:$verb_vform] ⊂ ⇓1;
  ↓3 = $Rest ∪ [aux_form:$aux_form, vtense:$verb_vform];
  ⇑ = ⇓1 ∪ [form:int];
}

/* **************************************************
 * Noun phrase
 ************************************************** */
@withspaces
//@bidirectional
NP → det N1 {
  [def:$def, number:$number, $Rest] ⊂ ⇓2;
  deferred(⇓2) {
    if (¬ $def)
      $def = true;
    if (¬ $number)
      $number = sg;
    ↓1 = [def:$def, number:$number, $Rest];
  }
  ↓2 = ↑;
  ⇑ = ⇓1 ∪ ⇓2 ∪ [def:$def, @_3];
}

@withspaces
NP → proper_noun {
  ↓1 = ↑;
  ⇑ = ⇓1;
}

NP → pro {
  ↓1 = ↑;
  ⇑ = ⇓1;
}

N1 → N {
  [MOD:NIL, $Rest];
  ↓1 = ↑; 
  ⇑ = ⇓1;
}

@withspaces
N1 → [adjQuality] N2 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjQuality;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

N2 → [adjSize] N3 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjSize;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N3 → [adjAge] N4 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjAge;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N4 → [adjShape] N5 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjShape;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N5 → [adjColor] N6 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjColor;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N6 → [adjOrigin] N7 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjOrigin;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N7 → [adjMaterial] N8 {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjMaterial;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = ↑; 
  ⇑ = ⇓2;
}

@withspaces
N8 → [adjPurpose] N {
  [MOD:$List, $Rest];
  foreach $Adj in $List {
      $Entries = search $Adj on adjPurpose;
      if (($Entries ≠ <>))
        ↓1 = $Adj;
  }
  attest (↓1 => #1);
  ↓2 = $Rest; 
  ⇑ = ⇓2;
}

N → common_noun {
  [MOD:NIL, $Rest];
  ↓1 = $Rest;
  ⇑ = ⇓1 ∪ ↑;
}

/* **************************************************
 * Verb and its complements
 ************************************************** */
@withspaces
//@permutable
VP → VN [NP] [part] [NP] [AP] [PP] [SCOMP|VCOMP] {
  // Given Arguments
  [OBJECT:$OBJECT, OBLIQUE:$OBLIQUE, OBJ_THETA:$OBJ_THETA, PRED_ADJ:$PRED_ADJ, SCOMP:$SCOMP, VCOMP:$VCOMP, lexical_function:$lexical_function, $Rest] ⊂ ↑;
  // Idiomatic Arguments
  [OBJECT:$Synth_OBJECT, OBLIQUE:$Synth_OBLIQUE, OBJ_THETA:$Synth_OBJ_THETA, PRED_ADJ:$Synth_PRED_ADJ, SCOMP:$Synth_SCOMP, VCOMP:$Synth_VCOMP, particule:$particle] ⊂ ⇓1;
  ↓1 = $Rest;
  
  if (#2) {
    if ($Synth_OBJECT)
      $OBJECT = $OBJECT ∪ $Synth_OBJECT; 
    ↓2 = [case:accusative, lexical_function:$lexical_function, $OBJECT];
  }
  else {
    attest (¬$OBJECT ∧ ¬$Synth_OBJECT);
  }
  
  if (#3) {
    attest ($particle);
    ↓3 = [particle:$particle];
  }
  else {
    attest (¬$particle);
  }
  
  if (#4) {
    if ($Synth_OBLIQUE)
      $OBLIQUE = $OBLIQUE ∪ $Synth_OBLIQUE; 
    ↓4 = [lexical_function:$lexical_function, $OBLIQUE];
  }
  else {
    attest (¬$OBLIQUE ∧ ¬$Synth_OBLIQUE);
  }
  
  if (#5) {
    if ($Synth_PRED_ADJ)
      $PRED_ADJ = $PRED_ADJ ∪ $Synth_PRED_ADJ; 
    ↓5 = [lexical_function:$lexical_function, $PRED_ADJ];
  }
  else {
    attest (¬$PRED_ADJ ∧ ¬$Synth_PRED_ADJ);
  }

  if (#6) {
    [pcase:$pcase] ⊂ ⇓1;
    if ($Synth_OBJ_THETA)
      $OBJ_THETA = $OBJ_THETA ∪ $Synth_OBJ_THETA; 
    ↓6 = [lexical_function:$lexical_function, pcase:$pcase, $OBJ_THETA];
  }
  else {
    attest (¬$OBJ_THETA ∧ ¬$Synth_OBJ_THETA);
  }
  
  if (#7.1) {
    [compl:$compl] ⊂ ⇓1;
    if ($Synth_SCOMP)
      $SCOMP = $SCOMP ∪ $Synth_SCOMP; 
    ↓7 = [lexical_function:$lexical_function, compl:$compl, $SCOMP];
  }
  else if (#7.2) {
    if ($Synth_VCOMP)
      $VCOMP = $VCOMP ∪ $Synth_VCOMP; 
    ↓7 = [lexical_function:$lexical_function, $VCOMP];
  }
  else {
    attest (¬$SCOMP ∧ ¬$Synth_SCOMP);
    attest (¬$VCOMP ∧ ¬$Synth_VCOMP);
  }
  
  ⇑ = ⇓1;
}

/* **************************************************
 * Verb 
 ************************************************** */
VN → VERB {
  [OBJECT:NIL, OBLIQUE:NIL, OBJ_THETA:NIL, PRED_ADJ:NIL, SCOMP:NIL, VCOMP:NIL, $Rest];
  ↓1 = $Rest;
  ⇑ = ⇓1;
}

/* **************************************************
 * Simple Verb 
 ************************************************** */
// he feels good
// does he [not] feel good
// he felt good
// did he [not] feel good
// is he [not] feeling good
// has he [not] felt good
// will he [not] feel good
// was he [not] feeling good
// had he [not] felt good
@withspaces
VERB → [not] verb {
  [verbal_constr:$verbal_constr, aux_form:none, $Rest] ⊂ ↑;
  if (#1) {
    attest (($verbal_constr == neg));
    ↓1 = [];
    ↓2 = $Rest ∪ [vtense:infinitive|past_participle|present_participle];
  } else {
    attest (($verbal_constr == no_neg));
    ↓2 = $Rest ∪ [vtense:present|preterite|infinitive|past_participle|present_participle];
  }
  ⇑ = ⇓2;
}

// passive voice
// he was accepted
// he was accepted by the compagny
zzzVERB → verb {
  [verbal_constr:$verbal_constr, aux_form:none, $Rest] ⊂ ↑;
  if (#1) {
    attest (($verbal_constr == neg));
    ↓1 = [];
    ↓2 = $Rest ∪ [vtense:infinitive|past_participle|present_participle];
  } else {
    attest (($verbal_constr == no_neg));
    ↓2 = $Rest ∪ [vtense:present|preterite|infinitive|past_participle|present_participle];
  }
  ⇑ = ⇓2;
}

// will he [not] have felt good
// has he [not] been feeling good
// had he [not] been feeling good
@withspaces
VERB → [not] have|be|been|have_been verb {
  [aux_form:have|be|been|have_been];
  [aux_form:_, verbal_constr:$verbal_constr, $Rest] ⊂ ↑;
  if (#1) {
    attest (($verbal_constr == neg));
    ↓1 = [];
  } else {
    attest (($verbal_constr == no_neg));
  }
  ↓3 = $Rest ∪ [vtense:infinitive|past_participle|present_participle];
  [aux_form:$aux_form] ⊂ ↑;
  [aux_form:$synth_aux_form] ⊂ ⇓3;
  ↓2 = [aux_form:$aux_form] ∪ [aux_form:$synth_aux_form];
  ⇑ = ⇓3;
}

// he doesn't feel good
// he didn't feel good
// he is [not] feeling good
// he has [not] felt good
// he will [not] feel good
// he was [not] feeling good
// he had [not] felt good

// he didn't feel good
// he didn't feel good
// he didn't feel good
// he will feel good

// he will have [not] felt good
@withspaces
VERB → aux [not] [have|be|been|have_been] verb {
  ↓1 = ↑;
  [verbal_constr:$verbal_constr, aux_form:$aux_form] ⊂ ↑;
  //attest ((¬ $verbal_constr) ∨ ($verbal_constr == int|neg));
  [vtense:_, tensed:_, $Rest] ⊂ ↑;
  [verbal_constr:$synth_verbal_constr, aux_form:$synth_aux_form, verb_vform:$verb_vform] ⊂ ⇓1;
  if (#2) {
    attest (($verbal_constr == neg));
    ↓2 = [];
  } else {
    attest (($verbal_constr == no_neg));
  }
  if (#3) {
    attest ($synth_aux_form);
    ↓3 = [aux_form:$aux_form] ∪ [aux_form:$synth_aux_form];
  }
  else {
    attest ((¬ $synth_aux_form) ∨ ($synth_aux_form == none));
  }
  ↓4 = $Rest ∪ [vtense:$verb_vform];
  [vtense:_, tensed:_, $Rest4] ⊂ ⇓4;
  ⇑ = ⇓1 ∪ $Rest4;
}

/* **************************************************
 * Adjective phrase
 ************************************************** */
@withspaces
//@trace
AP → [adv] adj {
  ↓2 = ↑;
  [MOD:<$MOD>] ⊂ ↑;
  deferred(⇓2) {
    [MOD:<$Synth_MOD>] ⊂ ⇓2;
    if (#1) {
      attest ($MOD ∨ $Synth_MOD);
      if ($MOD ∧ $Synth_MOD)
        ↓1 = $Synth_MOD ∪ $MOD;
      else if ($MOD)
        ↓1 = $MOD;
      else if ($Synth_MOD)
        ↓1 = $Synth_MOD;
    }  else {
      // Bug if $Synth_MOD doesn't exist
      //attest ((¬ ($MOD)) ∧ (¬ ($Synth_MOD)));
      attest (¬ ($MOD));
      attest (¬ ($Synth_MOD));
    }
  }
  ⇑ = ⇓2;
}

/* **************************************************
 * Prep phrase
 ************************************************** */
@withspaces
PP → prep NP {
  ↓1 = ↑;
  ↓2 = ↑;
  ⇑ = ⇓2;
}

/* **************************************************
 * 
 ************************************************** */
@withspaces
SCOMP → csu S_tense {
  ↓1 = ↑;
  [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest] ⊂ ↑;
  if (¬ $lexical_function)
    $lexical_function = none;
  if (¬ $tense)
    $tense = present;
  if (¬ $verbal_constr)
    $verbal_constr = no_int | no_neg;
  ↓2 = [lexical_function:$lexical_function, tense:$tense, verbal_constr:$verbal_constr, $Rest];
  ⇑ = ⇓2;
}

/* **************************************************
 * 
 ************************************************** */
@withspaces
VCOMP → to S {
  ↓1 = [];
  [lexical_function:$lexical_function, verbal_constr:$verbal_constr, $Rest] ⊂ ↑;
  if (¬ $lexical_function)
    $lexical_function = none;
  // not interrogative, not negative
  if (¬ $verbal_constr)
    $verbal_constr = no_int | no_neg;
  ↓2 = [lexical_function:$lexical_function, vtense:infinitive, verbal_constr:$verbal_constr, $Rest];
  ⇑ = ⇓1;
}
